---
title: "임베디드 OS 개발 프로젝트 4장 :: 부팅하기(2)"
excerpt: "익셉션 핸들러 설계"

toc: true # Table of Contents
toc_sticky: true # TOC를 고정해주는 역할 
# toc_label: # TOC의 제목 설정

categories:
  - Embedded RTOS
tags:
  - Embedded
  - RTOS
last_modified_at: 2021-07-13
---

<br/>

# 4.3 익셉션 핸들러 만들기

- 가장 먼저 리셋 익셉션 핸들러를 작성한다. 리셋 핸들러에서 수행하는 작업은 두 가지다.

- - 동작 모드별 스택 주소를 각 동작 모드의 뱅크드 레지스터 SP에 설정한다. 
  - 동작 모드별 스택이 모두 설정되고 나면 C언어 main() 함수로 진입한다.

- ARM은 동작 모드가 7개 있고, 이중 USR 모드와 SYS 모드는 레지스터를 공유하므로 SP 레지스터는 총 6개가 뱅크드 레지스터로 제공된다. 

- 리셋 익셉션 핸들러에서 동작 모드를 순서대로 변경해 가며 SP 레지스터에 정해진 값을 넣는 작업을 수행한다. 이러면 각 동작 모드의 스택이 초기화 되는 것이다.

```bash
vi MemoryMap.h
```

```c
#define INST_ADDR_START     0
#define USRSYS_STACK_START  0x00100000
#define SVC_STACK_START     0x00300000
#define IRQ_STACK_START     0x00400000
#define FIQ_STACK_START     0x00500000
#define ABT_STACK_START     0x00600000
#define UND_STACK_START     0x00700000
#define TASK_STACK_START    0x00800000
#define GLOBAL_ADDR_START   0x04800000
#define DALLOC_ADDR_START   0x04900000

#define INST_MEM_SIZE       (USRSYS_STACK_START - INST_ADDR_START)
#define USRSYS_STACK_SIZE   (SVC_STACK_START - USRSYS_STACK_START)
#define SVC_STACK_SIZE      (IRQ_STACK_START - SVC_STACK_START)
#define IRQ_STACK_SIZE      (FIQ_STACK_START - IRQ_STACK_START)
#define FIQ_STACK_SIZE      (ABT_STACK_START - FIQ_STACK_START)
#define ABT_STACK_SIZE      (UND_STACK_START - ABT_STACK_START)
#define UND_STACK_SIZE      (TASK_STACK_START - UND_STACK_START)
#define TASK_STACK_SIZE     (GLOBAL_ADDR_START - TASK_STACK_START)
#define DALLOC_MEM_SIZE     (55 * 1024 * 1024)

#define USRSYS_STACK_TOP    (USRSYS_STACK_START + USRSYS_STACK_SIZE - 4)
#define SVC_STACK_TOP       (SVC_STACK_START + SVC_STACK_SIZE - 4)
#define IRQ_STACK_TOP       (IRQ_STACK_START + IRQ_STACK_SIZE - 4)
#define FIQ_STACK_TOP       (FIQ_STACK_START + FIQ_STACK_SIZE - 4)
#define ABT_STACK_TOP       (ABT_STACK_START + ABT_STACK_SIZE - 4)
#define UND_STACK_TOP       (UND_STACK_START + UND_STACK_SIZE - 4)
```

- 각 동작 모드의 스택 시작 메모리 주소와 스택 크기, 그리고 스택 꼭대기 메모리 주소를 의하고 있다.

``` bash
vi ARMv7AR.h
```

```c
/* PSR Mode Bit Values */
#define ARM_MODE_BIT_USR 0x10
#define ARM_MODE_BIT_FIQ 0x11
#define ARM_MODE_BIT_IRQ 0x12
#define ARM_MODE_BIT_SVC 0x13
#define ARM_MODE_BIT_ABT 0x17
#define ARM_MODE_BIT_UND 0x1B
#define ARM_MODE_BIT_SYS 0x1F
#define ARM_MODE_BIT_MON 0x16
```

- ARM의 CPSR에 값을 설정하여 동작 모드를 바꿀 수 있는 값을 정의하고 있다.

```bash
vi Entry.S
```

```c
#include "ARMv7AR.h"
#include "MemoryMap.h"

.text
	.code 32

	.global vector_start
	.global vector_end

	vector_start:
	 LDR PC, reset_handler_addr
	 LDR PC, undef_handler_addr
	 LDR PC, svc_handler_addr
	 LDR PC, pftch_abt_handler_addr
	 LDR PC, data_abt_handler_addr
	 B .
	 LDR PC, irq_handler_addr
	 LDR PC, fiq_handler_addr

	 reset_handler_addr:		.word reset_handler
	 undef_handler_addr:		.word dummy_handler
	 svc_handler_addr:		.word dummy_handler
	 pftch_abt_handler_addr:	.word dummy_handler
	 data_abt_handler_addr:		.word dummy_handler
	 irq_handler_addr:		.word dummy_handler
	 fiq_handler_addr:		.word dummy_handler	
	vector_end:

	reset_handler:
		MRS r0, cpsr
		BIC r1, r0, #0x1F
		ORR r1, r1, #ARM_MODE_BIT_SVC
		MSR cpsr, r1
		LDR sp, =SVC_STACK_TOP
		
		MRS r0, cpsr
		BIC r1, r0, #0x1F
		ORR r1, r1, #ARM_MODE_BIT_IRQ
		MSR cpsr, r1
		LDR sp, =IRQ_STACK_TOP

        MRS r0, cpsr
		BIC r1, r0, #0x1F
		ORR r1, r1, #ARM_MODE_BIT_FIQ
		MSR cpsr, r1
		LDR sp, =FIQ_STACK_TOP

        MRS r0, cpsr
		BIC r1, r0, #0x1F
		ORR r1, r1, #ARM_MODE_BIT_ABT
		MSR cpsr, r1
		LDR sp, =ABT_STACK_TOP

        MRS r0, cpsr
		BIC r1, r0, #0x1F
		ORR r1, r1, #ARM_MODE_BIT_UND
		MSR cpsr, r1
		LDR sp, =UND_STACK_TOP

        MRS r0, cpsr
		BIC r1, r0, #0x1F
		ORR r1, r1, #ARM_MODE_BIT_SYS
		MSR cpsr, r1
		LDR sp, =USRSYS_STACK_TOP

	dummy_handler:
	           B .
.end
```

- 어셈블리어 코드도 헤더 파일(.h)을 포함하는 문법은 C언어와 비슷하다.

- 30 ~ 64번째 줄이 모든 동작 모드를 한 번씩 순회하면서 스택 꼭대기 메모리 주소를 SP에 설정하는 코드이다. 길지만 코드 다섯 줄을 반복하는 것이다.

- - **MRS r0, cpsr** : 상태 레지스터 cpsr의 값을 레지스터 r0에 넣어준다.
  - **BIC r1, r0, #0x1F** : BIC(Logical AND NOT or bit clear)는 해당 비트를 클리어한다. 논리 연산 식은 r1 = r0 & ~0x1F 이다. ~0x1F는 ~(00011111)이므로 r0의 bit[4:0]을 0으로 clear한다.
  - **ORR r1, r1, #****동작모드** : ORR(Logical OR or bit set)는 OR 연산을 한다. 연산 식은 r1 = r1 || #동작모드 이다. 
  - **MSR cpsr, r1** : 레지스터 r1의 값을 상태 레지스터 cpsr에 넣어준다. 이때 r1에 저장된 동작모드로 모드가 변환된다.
  - **LDR sp, =****스택** **꼭대기** **메모리** **주소** : 변환된 동작 모드의 뱅크드 레지스터인 sp에 정의한 스택 꼭대기 주소 값을 넣으면서 해당 동작 모드의 스택 설정이 완룐된다.

>스택은 메모리와 반대로 높은 주소에서 낮은 주소로 이동하므로 꼭대기 주소를 사용한다. 마지막 4바이트를 빼준 이유는 스택과 스택 사이의 패딩(padding)을 넣어준 것이다.

```bash
vi Makefile
```

```makefile
ARCH = armv7-a
MCPU = cortex-a8

CC = arm-none-eabi-gcc
AS = arm-none-eabi-as
LD = arm-none-eabi-ld
OC = arm-none-eabi-objcopy

LINKER_SCRIPT = ./navilos.ld

ASM_SRCS = $(wildcard boot/*.S)
ASM_OBJS = $(patsubst boot/%.S, build/%.o, $(ASM_SRCS))

INC_DIRS = include

navilos = build/navilos.axf
navilos_bin = build/navilos.bin

.PHONY: all clean run debug gdb

all: $(navilos)

clean:
	@rm -fr build

run: $(navilos)
	qemu-system-arm -M realview-pb-a8 -kernel $(navilos)

debug: $(navilos)
	qemu-system-arm -M realview-pb-a8 -kernel $(navilos) -S -gdb tcp::1234,ipv4

gdb: 
	gdb-multiarch

$(navilos): $(ASM_OBJS) $(LINKER_SCRIPT)
	$(LD) -n -T $(LINKER_SCRIPT) -o $(navilos) $(ASM_OBJS)
	$(OC) -O binary $(navilos) $(navilos_bin)

build/%.o: boot/%.S
	mkdir -p $(shell dirname $@)
	$(CC) -march=$(ARCH) -mcpu=$(MCPU) -I $(INC_DIRS) -c -g -o $@ $<
```

- Entry.S 파일을 이전과 비슷하게 build를 할 경우 문제가 생긴다. Makefile을 위와 같이 수정해줘야 문제가 발생하지 않는다. 

- - 헤더 파일이 있는 디렉터리 경로 정보를 어셈블리에 알려줘야 한다. 헤더 파일 디렉터리 경로를 INC_DIRS라는 변수로 정의 해주고, 컴파일 할 때 -I $(INC_DIRS)를 추가하여 헤더 파일 디렉터리 경로를 지시해준다.
  - C언어에서 #define 구문은 전처리기(preprocessor)에 의해 처리된다. arm-none-eabi-as는 어셈블러일 뿐이고 전처리는 해 주지 않는다. 전처리까지 하려면 arm-none-eabi-gcc를 사용해야 한다. 컴파일러를 $(AS)에서 $(CC)로 수정해준다.
  - Arm-none-eabi-as 대신 arm-none-eabi-gcc를 사용했으므로 옵션도 gcc에 맞춰야 한다. gcc는 기본적으로 실행 파일을 만들지만, 현재 명령의 목적은 오브젝트 파일을 만드는 것이다. 따라서 오브젝트 파일을 만들라고 지시하는 -c 옵션을 추가한다.

```bash
make all
make debug
make gdb
(gdb) target remote:1234
(gdb) file build/navilos.axf 
(gdb) s
```

- 명령어 한 줄 실행.
- 출력되는 값은 다음에 실행할 명령어를 나타낸다.

```bash
(gdb) i r
```

- 5번씩 s 명령어를 실행하면서 sp와 cpsr을 확인하여 각 모드의 스택 값이 잘 설정되었지 확인한다. 
- 마지막 동작 모드는 SYS이다. SYS 동작 모드를 제일 마지막에 설정한 이유는 스택 설정을 끝내면 RTOS로 진입할 것인데 RTOS와 펌웨어의 기본 동작 모드가 SYS이다. 추가 설정 작업 없이 SYS모드로 다음 작업을 계속 이어가기 위함이다.
